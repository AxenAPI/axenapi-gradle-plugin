plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

group 'pro.axenix-innovation.plugin'
sourceCompatibility = '11'

String repoURL = ""
if (version.endsWith('-SNAPSHOT')) {
    repoURL = System.getenv('NEXUS_SERVER_URL_SNAPSHOT').toString()
} else {
    repoURL = System.getenv('NEXUS_SERVER_URL_RELEASE').toString()
}

String nexus_user = System.getenv('NEXUS_USER')
String nexus_password = System.getenv('NEXUS_PASSWORD')

gradlePlugin {
    plugins {
        create("axenapi-generator-plugin"){
            id = 'axenapi-generator-plugin'
            implementationClass = 'KafkaCodegenPlugin'
            version = version
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        allowInsecureProtocol = true
        url repoURL
        credentials.setUsername(nexus_user)
        credentials.setPassword(nexus_password)
    }
}

boolean isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign(publishing.publications)
}

afterEvaluate {
     publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId project.name
                groupId project.group
                version version
                from components.java
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/AxenAPI/axenapi-gradle-plugin")
                credentials.setUsername(project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER"))
                credentials.setPassword(project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN"))
            }
            maven {
                allowInsecureProtocol = true
                url repoURL
                credentials.setUsername(nexus_user)
                credentials.setPassword(nexus_password)
            }
        }
    }
}

dependencies {
    implementation 'org.openapitools:openapi-generator:6.3.0'
    implementation 'pro.axenix-innovation:axenapi-generator:1.0.1'
}

test {
    useJUnitPlatform()
}